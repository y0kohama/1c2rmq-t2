
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
		
// Возвращает параметры брокера
// 
// Параметры:
// 	Брокер - СправочникСсылка.ИнтеграцияRMQБрокеры - Ссылка на брокера
// Возвращаемое значение:
// 	
Функция ПараметрыБрокера(Брокер) Экспорт
	
	Результат = Новый Структура; 

	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ИнтеграцияRMQБрокеры.Ссылка КАК Брокер,
	|	ИнтеграцияRMQБрокеры.Сервер,
	|	ИнтеграцияRMQБрокеры.Порт,
	|	ИнтеграцияRMQБрокеры.ИмяВиртуальногоХоста,
	|	ИнтеграцияRMQБрокеры.Логин
	|ИЗ
	|	Справочник.ИнтеграцияRMQБрокеры КАК ИнтеграцияRMQБрокеры
	|ГДЕ
	|	ИнтеграцияRMQБрокеры.Ссылка = &Ссылка";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Брокер);
	
	РезультатЗапроса = Запрос.Выполнить();
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Результат.Вставить(Колонка.Имя);		
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	Иначе
		Результат.Брокер = Справочники.ИнтеграцияRMQБрокеры.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
	
КонецФункции

#КонецОбласти

#КонецЕсли