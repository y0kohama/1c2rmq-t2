#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
    	Возврат;
	КонецЕсли;
	
	Сервер = СокрЛП(Сервер);
	ИмяВиртуальногоХоста = СокрЛП(ИмяВиртуальногоХоста);
	Логин = СокрЛП(Логин);

#Область ПроверкаДублей
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ИнтеграцияRMQБрокеры.Ссылка
	|ИЗ
	|	Справочник.ИнтеграцияRMQБрокеры КАК ИнтеграцияRMQБрокеры
	|ГДЕ
	|	ИнтеграцияRMQБрокеры.Ссылка <> &Ссылка
	|	И ИнтеграцияRMQБрокеры.Сервер = &Сервер
	|	И ИнтеграцияRMQБрокеры.Порт = &Порт
	|	И ИнтеграцияRMQБрокеры.ИмяВиртуальногоХоста = &ИмяВиртуальногоХоста";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Сервер", Сервер);
	Запрос.УстановитьПараметр("Порт", Порт);
	Запрос.УстановитьПараметр("ИмяВиртуальногоХоста", ИмяВиртуальногоХоста);

	Выборка = Запрос.Выполнить().Выбрать();
	ШаблонСообщения = НСтр("ru = 'Уже существует брокер с параметрами: сервер %1, порт %2, хост %3'");
	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = ИнтеграцияRMQКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения, 
			Сервер, 
			Формат(Порт, "ЧГ=;"),
			ИмяВиртуальногоХоста);
		ИнтеграцияRMQКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Сервер",, Отказ);
		
	КонецЦикла;
#КонецОбласти 
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли