#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
    	Возврат;
	КонецЕсли;
		
	Имя = СокрЛ(Имя);
	
#Область ПроверкаДублей
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ИнтеграцияRMQТочкиОбмена.Ссылка
	|ИЗ
	|	Справочник.ИнтеграцияRMQТочкиОбмена КАК ИнтеграцияRMQТочкиОбмена
	|ГДЕ
	|	ИнтеграцияRMQТочкиОбмена.Имя = &Имя
	|	И ИнтеграцияRMQТочкиОбмена.Владелец = &Владелец
	|	И ИнтеграцияRMQТочкиОбмена.Ссылка <> &Ссылка";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Имя", Имя);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ШаблонСообщения = НСтр("ru = 'Для брокера %1 уже существует точка обмена с именем %2'");
	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = ИнтеграцияRMQКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			Владелец, Имя);
		ИнтеграцияRMQКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Имя",, Отказ);	
		
	КонецЦикла;
#КонецОбласти
	
КонецПроцедуры

#КонецОбласти
		
#КонецЕсли